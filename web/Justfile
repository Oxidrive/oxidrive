build: setup
    #!/usr/bin/env sh
    cd app
    npx tailwindcss -i ./input.css -o ./assets/styles.css
    dx build --release

clean:
    rm -rf app/dist

watch: setup
    #!/usr/bin/env sh
    cd app
    npx concurrently "just watch-app" "just watch-styles"

watch-app: setup
    # https://github.com/DioxusLabs/dioxus/issues/2411
    # dx serve --hot-reload --bin oxidrive
    cargo watch -s 'dx build --bin oxidrive'

watch-styles: setup
    #!/usr/bin/env sh
    cd app
    npx tailwindcss -i ./input.css -o ./assets/styles.css --watch

test:
    cargo nextest run --all --all-features --all-targets

setup:
    [ -d ../node_modules ] || npm install

lint: check clippy fmt-check

clippy:
    cargo clippy --workspace --tests --all-features --all-targets -- -D warnings

fmt:
    dx fmt
    cargo fmt --all
    cargo clippy --allow-dirty --allow-staged --workspace --fix -- -D warnings

fmt-check:
    cargo fmt --all -- --check

check:
    cargo check --all --tests --all-features --all-targets

generate-models:
    docker run \
      -u=$(id -u):$(id -g) \
      --rm \
      -v $PWD/..:/local \
      openapitools/openapi-generator-cli \
      generate \
      -i /local/server/openapi/out.yml \
      -g rust \
      -o /local/web/api/ \
      --global-property models,modelDocs=false \
      --additional-properties=avoidBoxedModels=true,bestFitInt=true,preferUnsignedInt=true \
      --skip-validate-spec
