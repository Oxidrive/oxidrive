set dotenv-filename := "../server/.env"

test *args: _install
    npx playwright test {{ args }}

chromium: (test "--project=chromium")

ui: _install
    npx playwright test --ui

start:
    #!/usr/bin/env sh
    bin="${OXIDRIVE_BIN:-$PWD/../server/bin/oxidrive}"
    $bin --public-url=http://localhost:4000

record url="http://localhost:8080": _install
    npx playwright codegen {{ url }}

fmt: _install
    npx biome format --write .

lint: _install
    npx biome ci .

[linux]
setup: _install
    #!/usr/bin/env sh
    if grep -q ID=nixos /etc/os-release; then
      echo ""
      echo "====== WARNING ======="
      echo "Playwright is not able to install browser on NixOS."
      echo "They should be automatically installed by the project's flake.nix, but until this PR is merged only Chromium will be available."
      echo "To use them, set the following environment variable: PLAYWRIGHT_BROWSERS_PATH=$NIX_PLAYWRIGHT_BROWSERS_PATH"
      echo "Please run tests with just e2e/chromium to avoid failures due to missing browsers"
      echo ""
      echo "https://github.com/NixOS/nixpkgs/pull/298944"
    else
      npx playwright install --with-deps
    fi
    mkdir -p playwright/.auth

[macos, windows]
setup: _install
    npx playwright install --with-deps
    mkdir -p playwright/.auth

_install:
    [ -d ../node_modules ] || npm ci
