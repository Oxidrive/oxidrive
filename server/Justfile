set dotenv-load

build: generate
    go build -o ./bin/oxidrive ./cmd/oxidrive/

run *args: generate fmt
    go run ./cmd/oxidrive {{ args }}

test path="./...": generate
    @if command -v gotestsum &> /dev/null; then \
      gotestsum --format gotestdox {{ path }}; \
    fi
    @if ! command -v gotestsum &> /dev/null; then \
      gotestdox {{ path }}; \
    fi

    cd .. && gremlins run ./server/internal --test-cpu=2 --workers=2

test-integration:
    INTEGRATION_TEST=1 just test

watch: fmt
    air

generate: openapi
    go generate ./...

openapi:
    rm -f -- openapi/out.yml
    npx @redocly/cli@latest join -o openapi/out.yml openapi/openapi.yaml ./openapi/*.yaml

fmt:
    goimports -local github.com/oxidrive/oxidrive -w .
    go fmt ./...

psql:
    @PGPASSWORD=$POSTGRES_PASSWORD psql -h $POSTGRES_HOST -p $POSTGRES_PORT -U $POSTGRES_USER $POSTGRES_DB

migration-create name:
    @for db in "postgres" "sqlite"; do \
      migrate create -dir "./migrations/$db" -ext sql {{ name }}; \
    done
