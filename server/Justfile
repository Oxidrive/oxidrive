set dotenv-load

build: generate
    go build -o ./bin/oxidrive ./cmd/oxidrive/

run *args: generate
    go run ./cmd/oxidrive {{ args }}

test path="./...":
    @if command -v gotestsum &> /dev/null; then \
      gotestsum --format gotestdox {{ path }}; \
    fi
    @if ! command -v gotestsum &> /dev/null; then \
      gotestdox {{ path }}; \
    fi

test-mutations:
    cd .. && INTEGRATION_TEST=1 gremlins run ./server/internal --test-cpu=2 --workers=2

test-integration *args:
    INTEGRATION_TEST=1 just test {{ args }}

watch:
    air

generate: openapi
    go generate ./...

openapi:
    rm -f -- openapi/out.yml
    npx @redocly/cli@latest join -o openapi/out.yml openapi/openapi.yaml ./openapi/*.yaml

fmt:
    goimports -local github.com/oxidrive/oxidrive -w .
    go fmt ./...

psql:
    @PGPASSWORD=${POSTGRES_PASSWORD:-oxidrive} psql -h ${POSTGRES_HOST:-localhost} -p ${POSTGRES_PORT:-5432} -U ${POSTGRES_USER:-oxidrive} ${POSTGRES_DB:-oxidrive}

migration-create name:
    @for db in "postgres" "sqlite"; do \
      migrate create -dir "./migrations/$db" -ext sql {{ name }}; \
    done
